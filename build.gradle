plugins {
  id 'com.github.sherter.google-java-format' version '0.8'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

sourceCompatibility = 1.8
version = '0.0.1'

repositories {
    mavenCentral()
}
configurations {
  checkstyleConfig
}

def versions = [
  checkstyle: '8.23',
  findbugs: '3.0.1'
]
dependencies {
    testCompile 'junit:junit:4.8.2'
    checkstyleConfig("com.puppycrawl.tools:checkstyle:${versions.checkstyle}") {
        transitive = false
    }
    findbugs "com.google.code.findbugs:findbugs:${versions.findbugs}"
}


sourceSets {
    main {
        java {
            srcDir "src"
        }
        resources {
            srcDir 'resources'
        }
    }
    test {
        java {
            srcDirs = ["test"]
        }
    }
    
}

run {
    mainClassName = 'com.run.Main'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

tasks.withType(FindBugs) {
    it.group = 'verification'
    reports {
        xml.enabled false
        html.enabled true
    }
}

findbugs {
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
}

checkstyle {
  toolVersion = "${versions.checkstyle}"
  ignoreFailures = false
  maxWarnings = 0
  config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, "google_checks.xml")
}

tasks.withType(Checkstyle).each {
    it.group = 'verification'
}

tasks.withType(com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormat).each {
    it.group = 'format'
}

tasks.withType(com.github.sherter.googlejavaformatgradleplugin.VerifyGoogleJavaFormat).each {
    it.group = 'format'
}

check.dependsOn jacocoTestReport